dnl BKG_RPM
dnl -------
dnl
dnl Add rpm specific targets to the Makefile.
dnl
dnl Substitutes BKG_RPM_PKG_CONFIG_DEPS when macro BKG_SHARED_LIB_PKGCONFIG_DEPS
dnl is defined
dnl
AC_DEFUN([BKG_RPM], [
_BKG_RPM_OS=$(cat /etc/os-release|grep \^NAME|cut -d'=' -f2|xargs)

ifdef([BKG_SHARED_LIB_PKGCONFIG_DEPS], dnl
AC_SUBST([BKG_RPM_PKG_CONFIG_DEPS], m4_strip(m4_foreach([tmp], [BKG_SHARED_LIB_PKGCONFIG_DEPS], ["pkgconfig(m4_strip(tmp)) "]))))
AC_MSG_CHECKING([RPM Support])
AM_CONDITIONAL([BKG_RPM_CAP], [test "x$_BKG_RPM_OS" = "xFedora" || test "x$_BKG_RPM_OS" = "xCentos"])dnl
AM_COND_IF([BKG_RPM_CAP],
[
AC_MSG_RESULT(yes)


AC_MSG_CHECKING([RPM Package Manager])
_BKG_RPM_CMD=$([[ "$_BKG_RPM_OS" == "Fedora" ]] && echo dnf || echo yum)
m4_define([BKG_RPM_CMD], [$_BKG_RPM_CMD])
AC_MSG_RESULT(BKG_RPM_CMD)

AC_CHECK_PROG([RPMSPEC], [rpmspec], yes)
AS_IF([test -z "$RPMSPEC"], [AC_MSG_ERROR([Please install rpmspec(1)])])

AC_SUBST(BKG_RPM_SPECFILE, AC_PACKAGE_NAME.spec)
AC_CONFIG_FILES($BKG_RPM_SPECFILE)
AC_SUBST([BKG_RPM_DISTCLEAN], "distclean-rpm")
AC_SUBST([CONFIG_STATUS_DEPENDENCIES], [$CONFIG_STATUS_DEPENDENCIES' $(top_srcdir)/AC_PACKAGE_NAME.spec.in'])
AC_SUBST([BKG_RPM_TEMPLATE], "
# ┌─────────────────────────────┐
# │ Generated by: m4/BKG_RPM.m4 │
# │                             └──────────────────────────────────────────────┐

    WORKDIR     ?= \$(CURDIR)/build
    TOPDIR      ?= \$(WORKDIR)
    SPECDIR     ?= \$(CURDIR)
    SRCRPMDIR   ?= \$(CURDIR)/pkgs/srpm
    BUILDDIR    ?= \$(WORKDIR)
    RPMDIR      ?= \$(CURDIR)/pkgs/rpm
    SOURCEDIR   ::= \$(CURDIR)
    RPM_NAMES    = \$(shell rpm --specfile $BKG_RPM_SPECFILE \\
                        |grep -v '\-debuginfo\-'|tr \"\n\" ' ')
    RPM_FILES    = \$(shell \\
        rpm --specfile $BKG_RPM_SPECFILE \\
                --qf \"rpm/%{arch}/%{name}-%{version}-%{r}.%{arch}.rpm\\n\" \\
            |grep -v '\-debuginfo\-'|grep -v '\-debugsource\-'|tr \"\n\" ' ')

    RPM_DEFINES ::= --define \"_sourcedir \$(SOURCEDIR)\" \\
                   --define \"_specdir \$(SPECDIR)\"     \\
                   --define \"_builddir \$(BUILDDIR)\"   \\
                   --define \"_srcrpmdir \$(SRCRPMDIR)\" \\
                   --define \"_rpmdir \$(RPMDIR)\"       \\
                   --define \"_topdir \$(TOPDIR)\"       \\
                   --define \"debug_package %{nil}\"

.PHONY: dist-rpm
dist-rpm: \$(RPM_FILES)

.PHONY: distclean-rpm
distclean-rpm:
	rm -f \$(RPM_FILES)

.PHONY: install-rpm
install-rpm: \$(RPM_FILES)
	rpm -q \$(RPM_FILES) || \\
		sudo BKG_RPM_CMD install -y \$(RPM_FILES)

.PHONY: uninstall-rpm
uninstall-rpm:
	sudo BKG_RPM_CMD remove -y \$(RPM_NAMES) || true

\$(RPM_FILES): $BKG_RPM_SPECFILE
	sudo dnf builddep -y $BKG_RPM_SPECFILE
	rpmbuild \$(RPM_DEFINES) -bb $BKG_RPM_SPECFILE

# └────────────────────────────────────────────────────────────────────────────┘"
)
AM_SUBST_NOTMAKE(BKG_RPM_TEMPLATE)
], AC_MSG_RESULT([no]))
])

# vim: noexpandtab
